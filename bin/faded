#!/bin/zsh
setopt errexit errreturn

faded_d="$HOME/.config/faded"
mkdir -pv "$faded_d"

shim_path=${0:h}
self="${0:t}"

pip_deps=("$self")
bin="$self"

case "$self" in
	faded)
		cd "$shim_path"

		name="$1"; shift

		echo "*Shimmer*ing: $shim_path/$self ***> $shim_path/$name"
		if [[ ! -e "./$name" ]]; then
			ln -sv $self $name
		else
			echo "Not overwriting: $shim_path/$name" >&2
		fi

		# TODO update ?

		if [[ $# -gt 1 ]]; then
			echo "Saving config: $faded_d/$name"
			echo "$@" | tee "$faded_d/$name"
		elif [[ -e "$faded_d/$name" ]]; then
			echo "Removing config: $faded_d/$name"
			cat "$faded_d/$name"
			rm -fv "$faded_d/$name"
		fi

		exit
		;;
esac

args=()
if [[ -f "$faded_d/$self" ]]; then
	args+=($(cat "$faded_d/$self"))
fi

exec fades \
	${args[@]} \
	${=${(@)pip_deps/#/-d }} -x "$bin" \
	-- "$@"

