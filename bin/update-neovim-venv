#!/usr/bin/env zsh
set -eo pipefail

: ${NEOVIM_VENV:=$HOME/.virtualenvs/neovim}

# clear any possibly active venvs
export path=(${path#$HOME/.virtualenvs/*})

get-pyver()	{ ${1:-python} -c "import sys; print('%d.%d' % sys.version_info[:2])"; }

pre=()
if (( ${+commands[prefixout]} )); then
	pre+=(prefixout -dtc --)
fi

use-venv() {
	local venv=${1:?}
	export path=(
		$venv/bin
		$path
	)
	hash -r
}

if [[ ! -d $NEOVIM_VENV ]]; then

	rm -f $NEOVIM_VENV/bin/python
	$pre virtualenv-multiver $NEOVIM_VENV $(get-pyver python2)

	rm -f $NEOVIM_VENV/bin/python
	$pre virtualenv-multiver $NEOVIM_VENV $(get-pyver python3)

	ln -sfvr $NEOVIM_VENV/bin/python3 $NEOVIM_VENV/bin/python

	echo "-- Enabling global site packages for all vers"
	$pre rm -fv $NEOVIM_VENV/**/no-global-site-packages.txt
fi

use-venv $NEOVIM_VENV

nvim_pkgs=(
	neovim

	jedi
	rope ropevim
	typing
)
pip2 install --pre -U $nvim_pkgs

nvim3_pkgs+=(
	$nvim_pkgs
	neovim-gui
)
pip3 install --pre -U $nvim3_pkgs

tools_pkgs=(
	isort importmagic

	yapf autopep8

	pyflakes flake8 mccabe bandit
	pydocstyle pycodestyle

	mistune

	six psutil setproctitle

	'git+https://github.com/palantir/python-language-server@develop#egg=python-language-server'
	'pyls-mypy'
	'pyls-isort'
	#'python-language-server'
)
pip3 install --pre -U $tools_pkgs || :


# # fuck you ipython/pip, what the fuck is this shit?
# () {
# 	echo "-- Installing ipython according to pyver"

# 	local pip
# 	for pip in $pips; do
# 		pkg='ipython[notebook]'
# 		case $pip in
# 			pip2*) pkg="$pkg<6" ;;
# 		esac

# 		cmd=($pre $pip install --pre -U $pkg)
# 		echo "-- $cmd"
# 		$cmd
# 	done
# }
