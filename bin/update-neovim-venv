#!/usr/bin/env zsh
set -eo pipefail

: ${NEOVIM_VENV:=~/.virtualenvs/neovim}

pre=()
if (( ${+commands[prefixout]} )); then
	pre+=(prefixout -dtc --)
fi

if [[ ! -d $NEOVIM_VENV ]]; then
	echo "-- Creating neovim venv: $NEOVIM_VENV"

	hash -r
	pythons=(${commands[(I)python[2-3].[0-9]]})

	# default to the highest python baby
	def_py=${pythons[-1]}
	#pythons[-1]=()

	$pre virtualenv \
		-p $def_py \
		--system-site-packages \
		$NEOVIM_VENV

	echo "-- Installing virtualenv-multiver in venv"
	$pre sudo pip install -U virtualenv-multiver

	for py in $pythons; do
		ver=${py#python}
		ver_major=${ver:0:1}
		echo "-- Setting up alt python=$py ver=$ver ver_major=$ver_major in venv"

		# Hack because virtualenv-multiver is not good at it's job
		$pre rm -fv $NEOVIM_VENV/bin/{python,python${ver:0:1},python$ver}

		$pre virtualenv-multiver $NEOVIM_VENV $ver
	done

	echo "-- Enabling global site packages"
	$pre rm -fv $NEOVIM_VENV/**/no-global-site-packages.txt
fi

path=(
	$NEOVIM_VENV/bin
	$path
)

hash -r
pythons=(${commands[(I)python[2-3].[0-9]]})
python_vers=(${pythons#python})
pips=(pip${^python_vers})

echo "-- pythons=$pythons"
echo "-- python_vers=$python_vers"
echo "-- pips=$pips"

for cmd in $pythons $pips; do
	[[ ${commands[$cmd]} != $NEOVIM_VENV/* ]] || continue
	echo "-- Command \"$cmd\" not found in $NEOVIM_VENV" >&2
	exit 1
done

function pips() {
	local pip
	for pip in $pips; do
		command $pre $pip "$@" &
	done
	wait
}

pips install --pre -U neovim \
	jedi \
	rope ropevim \
	typing \
	isort yapf pyflakes flake8 mccabe bandit \
	mistune psutil setproctitle \
	greenlet gevent

$pre pip2 install --pre -U neovim-gui

pips install --pre -U 'git+https://github.com/palantir/python-language-server@develop#egg=python-language-server'

# fuck you ipython/pip, what the fuck is this shit?
() {
	echo "-- Installing ipython according to pyver"

	local pip
	for pip in $pips; do
		pkg='ipython[notebook]'
		case $pip in
			pip2*) pkg="$pkg<6" ;;
		esac

		cmd=($pre $pip install --pre -U $pkg)
		echo "-- $cmd"
		$cmd
	done
}
