#!/bin/zsh
##
## Why is this not the default??
## ~trevorj
##
set -eo pipefail
setopt no_function_arg_zero

# Remove self from path
path=(${path#$0:a:h})

args=("$@")

find-command() {
    local cmd
    for cmd in "$@"; do
        (( ${+commands[$cmd]} )) || continue

        case $found_cmd:A in
            $0:a|$0:A)
                echo "[$0:a] Refusing to fork bomb" >&2
                local self=($0 $0:a $0:A)
                declare -p cmd self path
                return 1
                ;;
        esac

        echo $cmd
        return
    done
    return 1
}

try() {
    local try_cmd="$1"; shift
    local found_cmd=()
    found_cmd=$(find-command $try_cmd) || return 1
    exec $found_cmd $args
    exit 0
}

# Over SSH, do not allow GUI
# https://gpgtools.tenderapp.com/kb/faq/enter-passphrase-with-pinentry-in-terminal-via-ssh-connection
if [[ -n $SSH_CONNECTION ]] ;then
    export GPG_TTY=$(tty)
    export PINENTRY_USER_DATA="USE_CURSES=1"
fi

case $OSTYPE:l in
    linux*)
        try 'pinentry-x11' || try 'pinentry'
        ;;
    darwin*)
        try 'pinentry-mac' \
            || try '/usr/local/MacGPG2/libexec/pinentry-mac.app/Contents/MacOS/pinentry-mac' \
            || try 'pinentry-x11' \
            || try 'pinentry'
        ;;
esac

echo "[$0:a] Could not find pinentry binary" >&2
exit 1

