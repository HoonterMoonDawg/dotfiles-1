#!/bin/zsh

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# Def
setopt extendedglob kshglob
setopt nullglob
setopt interactivecomments

umask 022

# Why in the world is this enabled by default on Linux? zgen?
zmodload -i zsh/stat
disable stat

# zgen
ZGEN_RESET_ON_CHANGE=(
	#{$LOCAL_ZDOTDIR,$ZDOTDIR}/zsh{env,rc,plugins}(.)
	{$LOCAL_ZDOTDIR,$ZDOTDIR}/zsh{env,rc,plugins}
)
source "$HOME/.zgen/zgen.zsh"

# plugin conf
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor root line)

# if the init scipt doesn't exist
if ! zgen saved; then
	echo "Creating a zgen save"

	# oh-my-shit
	#zgen oh-my-zsh
	#zgen prezto

	# oh-my-shit plugins (try to avoid)
	#zgen oh-my-zsh plugins/git
	#zgen oh-my-zsh plugins/sudo
	#zgen oh-my-zsh plugins/command-not-found

	# TODO check plugins difference on hook and auto-load

	# bulk load
	plugins=(
		$LOCAL_ZDOTDIR/plugins/*(/)
		$ZDOTDIR/plugins/*(/)
	)
	source-with-force {$LOCAL_ZDOTDIR,$ZDOTDIR}/zshplugins

	echo ${(F)plugins[@]} | zgen loadall

	# completions
	#zgen load zsh-users/zsh-completions src

	# theme
	#zgen oh-my-zsh themes/arrow

	# save all to init script
	zgen save
fi

# Why in the world is this enabled by default on Linux? zgen?
zmodload -u zsh/stat

##
## autoloads
##

function autoload-all-in() {
	local function_glob='^([_.]*|prompt_*_setup|README*)(-.N:t)'
	local p
	setopt LOCAL_OPTIONS EXTENDED_GLOB NULLGLOB
	for p in ${^argv}(/); do
		autoload -Uz $p/${~function_glob}
	done
}

autoload-all-in {$LOCAL_ZDOTDIR,$ZDOTDIR}/functions

autoload -Uz before after throw catch relative cdr sticky-note surround zcalc{,-auto-insert} which-command zcompdump zargs zed zed-set-file-name zfanon zfautocheck zfcd zfcd_match zfcget xtermctl
autoload -Uz allopt age checkmail chpwd_recent_{add,dirs,filehandler} calendar{,_{add,edit,lockfiles,parse,read,scandate,show,showdate,sort}}

autoload -Uz run-help{,-{git,ip,openssl,p4,sudo,svk,svn}}

autoload -Uz backward-kill-word-match backward-word-match bracketed-paste-url-magic capitalize-word-match copy-earlier-word quote-and-complete-word read-from-minibuffer regexp-replace replace-argument replace-string replace-string-again select-bracketed select-quoted select-word-style send-invisible smart-insert-last-word split-shell-arguments

# bash comp
autoload -Uz bashcompinit
bashcompinit

# Set up the prompt
#autoload -Uz promptinit
#promptinit
#prompt walters
source "$ZDOTDIR/themes/airline2.zsh"

# Hub
[[ ! ${+commands[hub]} ]] || eval "$(hub alias -s)"

# Google Cloud SDK
function () {
	local app_path="$1"
	[[ -d "$app_path" ]] || return
	source "$app_path/path.zsh.inc"
	source "$app_path/completion.zsh.inc"
} "$HOME/.local/google-cloud-sdk"

# This is annoying to have earlier.
#setopt warncreateglobal

# pyenv
#function () {
#	path=("$HOME/.pyenv/bin" ${path[@]})
#	eval "$(pyenv init -)"
#	eval "$(pyenv virtualenv-init -)"
#}

# local
# TODO Move to $XDG local path
source-with-force "$LOCAL_ZDOTDIR/zshrc"

