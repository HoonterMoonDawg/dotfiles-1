#!/bin/zsh

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# Def
setopt extendedglob kshglob
setopt nullglob
setopt interactivecomments

umask 022

# zgen
ZGEN_RESET_ON_CHANGE=(
	{$LOCAL_ZDOTDIR,$ZDOTDIR}/zsh{env,rc,plugins}(.)
)
source "$HOME/.zgen/zgen.zsh"

# plugin conf
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor root line)

# if the init scipt doesn't exist
if ! zgen saved; then
	echo "Creating a zgen save"

	# oh-my-shit
	#zgen oh-my-zsh
	#zgen prezto

	# oh-my-shit plugins (try to avoid)
	#zgen oh-my-zsh plugins/git
	#zgen oh-my-zsh plugins/sudo
	#zgen oh-my-zsh plugins/command-not-found

	# TODO check plugins difference on hook and auto-load

	# bulk load
	plugins=(
		$LOCAL_ZDOTDIR/plugins/*(/)
		$ZDOTDIR/plugins/*(/)
	)
	source-with-force {$LOCAL_ZDOTDIR,$ZDOTDIR}/zshplugins

	echo ${(F)plugins[@]} | zgen loadall

	# completions
	#zgen load zsh-users/zsh-completions src

	# theme
	#zgen oh-my-zsh themes/arrow

	# save all to init script
	zgen save
fi

# autoloads
autoload -Uz before after throw catch relative cdr sticky-note surround zcalc{,-auto-insert} which-command zcompdump zargs zed zed-set-file-name zfanon zfautocheck zfcd zfcd_match zfcget xtermctl
autoload -Uz allopt age checkmail chpwd_recent_{add,dirs,filehandler} calendar{,_{add,edit,lockfiles,parse,read,scandate,show,showdate,sort}}

autoload -Uz run-help{,-{git,ip,openssl,p4,sudo,svk,svn}}

autoload -Uz backward-kill-word-match backward-word-match bracketed-paste-url-magic capitalize-word-match copy-earlier-word quote-and-complete-word read-from-minibuffer regexp-replace replace-argument replace-string replace-string-again select-bracketed select-quoted select-word-style send-invisible smart-insert-last-word split-shell-arguments

# bash comp
autoload -Uz bashcompinit
bashcompinit

# Set up the prompt
autoload -Uz promptinit
promptinit
prompt walters

# Hub
[[ ! ${+commands[hub]} ]] || eval "$(hub alias -s)"

# Google Cloud SDK
function () {
	local app_path="$1"
	[[ -d "$app_path" ]] || return
	source "$app_path/path.zsh.inc"
	source "$app_path/completion.zsh.inc"
} "$HOME/.local/google-cloud-sdk"

# This is annoying to have earlier.
setopt warncreateglobal

# aliases
alias vim='nvim'
alias v='vim'
alias vi='vim'

alias grep='grep --color=auto'
alias -g G='| grep'
alias ls='ls --color=auto -Fuh -B -G'
alias l='ls'
alias la='ls -A'
alias ll='ls -l'
alias lg='ls -g'
alias lla='ll -A'

# fzf
function () {
	local fzf_path="$GOPATH/src/github.com/junegunn/fzf"
	[[ -d "$fzf_path" ]] || return
	manpath+=("$fzf_path/man")

	[[ $- == *i* ]] && source "$fzf_path/shell/completion.zsh" 2>/dev/null
	source "$fzf_path/shell/key-bindings.zsh"
}

# linuxbrew (for git builds)
function () {
	local brew_path="$HOME/.linuxbrew"
	[[ -d "$brew_path" ]] || return
	path+=("$brew_path"/{,s}bin)
	manpath+=("$brew_path/share/man")
	infopath+=("$brew_path/share/info")

	# always compile from source, using distro libc
	export HOMEBREW_BUILD_FROM_SOURCE=1
}

# pyenv
#function () {
#	path=("$HOME/.pyenv/bin" ${path[@]})
#	eval "$(pyenv init -)"
#	eval "$(pyenv virtualenv-init -)"
#}

# local
# TODO Move to $XDG local path
source-with-force "$LOCAL_ZDOTDIR/zshrc"

