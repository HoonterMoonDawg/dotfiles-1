#!/bin/zsh

umask 022

# if it's a dumb terminal, return.
if [[ ${TERM} == 'dumb' ]]; then
    return
fi

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# Def
setopt extendedglob kshglob
setopt nullglob
setopt interactivecomments

# Why in the world is this enabled by default on Linux? zgen?
zmodload -i zsh/stat
disable stat

# zgen
ZGEN_RESET_ON_CHANGE=(
	${^ZDOTDIRS}/zsh{env,rc,plugins}(-.)
)
source "$HOME/.zgen/zgen.zsh"

# plugin conf
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor root line)

source-with-force ~/.zsh/secrets

# if the init scipt doesn't exist
if ! zgen saved; then
	echo "-- Creating a zgen save"

	# TODO check plugins difference on hook and auto-load

	local plugin_dirs=(
		${^ZDOTDIRS}/plugins(-/)
	)
	echo "-- Plugin dirs:" $plugin_dirs

	source-with-force ${^ZDOTDIRS}/zshplugins(-.)

	echo "-- Plugins:" ${plugins#(${~${(j:|:)plugin_dirs}})/}
	echo ${(F)plugins[@]} | zgen loadall

	# save all to init script
	zgen save
fi

# Set up the prompt
case "$USERNAME" in
	root)
		autoload -Uz promptinit
		promptinit
		prompt walters
		;;
	*) source "$ZDOTDIR/themes/airline2.zsh" ;;
esac

# local
# TODO Move to $XDG local path
source-with-force $LOCAL_ZDOTDIR/zshrc

# This is annoying to have earlier.
#setopt warncreateglobal
