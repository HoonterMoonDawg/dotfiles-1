#!/bin/zsh

function add-prefix-paths {
	local path_dirs=(
		bin
		sbin
	)
	local completions_dirs=(
		completions
		#zsh/completions

		etc/zsh/completions

		share/zsh/vendor-functions
		# Lots of packages seem to still use this vs vendor-functions
		share/zsh/site-functions
	)
	local fpath_dirs=(
		functions
		#zsh/functions

		#etc/zsh/functions
	)

	local manpath_dirs=(
		share/man
		man
	)

	local infopath_dirs=(
		share/info
	)

	local prefixes=(${(@)argv:a})

	# prefix
	path=(${(@)path} ${^prefixes}/${^path_dirs}(-/N))
	fpath=(${(@)fpath} ${^prefixes}/${^completions_dirs}(-/N))
	fpath=(${(@)fpath} ${^prefixes}/${^fpath_dirs}(-/N))
	manpath=(${(@)manpath} ${^prefixes}/${^manpath_dirs}(-/N))
	infopath=(${(@)infopath} ${^prefixes}/${^infopath_dirs}(-/N))
}

add-prefix-paths "$@"
