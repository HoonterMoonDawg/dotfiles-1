#!/bin/zsh

umask 022

# if it's a dumb terminal, return.
if [[ ${TERM} == 'dumb' ]]; then
	return
fi
[[ $- =~ i ]] || return

##
## Queue loads via mocking them.
## We'll run them once we have the full list if necessary.
## Avoids races and dependency woes since now we can order accordingly.
##

# Reset zplug queue
typeset -a ZPLUGINS_Q=()

function zplugins.load() {
	set -- $1
	ZPLUGINS_Q+=(${(q)argv} $'\0')
}

##
## Zplugins loader API
##

function zplugins.load-all-in() {
	local p

	local paths=()
	for p in "$@"; do
		if [[ ${p:0:1} == / ]]; then
			paths+=($p(-/N))
		else
			paths+=(${^ZDOTDIRS}/$p(-/N))
		fi
	done

	local defer
	for p in ${^paths}/*(-/N); do
		defer=${${p:t}%%[^0-9]*}
		if [[ -n $defer ]]; then
			defer=$defer
		else
			defer=0
		fi

		zplugins.load $p --from local --defer $defer
	done
}

function zplugins.antibody.init() {
	if ! (( ${+commands[antibody]} )); then
		echo "- Antibody not found in path. Cannot/will not continue. Deal with it." >&2
		echo "  >> go get -u -x -v github.com/akatrevorjay/antibody" >&2
		return 1
	fi

	source <(command antibody init)
}

function zplugins.lazy.show() {
	command antibody bundle "${(0)ZPLUGINS_Q}"
}

function zplugins.lazy.apply() {
	antibody bundle "${(0)ZPLUGINS_Q}"
}

##
## Meat
##

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

if [[ -z $ZSH_CACHE_DIR ]]; then
	echo "- ZSH_CACHE_DIR is not set?" >&2
	return 1
elif [[ ! -e $ZSH_CACHE_DIR ]]; then
	echo "- Creating ZSH_CACHE_DIR=\"$ZSH_CACHE_DIR\"" >&2
	mkdir -pv $ZSH_CACHE_DIR
fi

# enable all of the highlighters
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor root)

# secrets
source-with-force ~/.zsh/secrets

# Set up the prompt
autoload -Uz ztheme; ztheme

# Plugin sources
ZPLUGINS_FILES=(${^ZDOTDIRS}/zshplugins(-.N))

# Collect plugin list
source-with-force $ZPLUGINS_FILES

# Now we can apply them as necessary upon first call transparently.
# Avoids races and dependency woes since now we can order accordingly.
zplugins.antibody.init
zplugins.lazy.apply

##
## Real mode
##

# local
source-with-force $LOCAL_ZDOTDIR/zshrc

# This is annoying to have earlier.
#setopt warncreateglobal

autoload -Uz fix-{f,}path-order
fix-path-order
fix-fpath-order

