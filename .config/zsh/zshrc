#!/bin/zsh

umask 022

# if it's a dumb terminal, return.
if [[ ${TERM} == 'dumb' ]]; then
    return
fi

# Use emacs keybindings even if our EDITOR is set to vi
bindkey -e

# Def
setopt extendedglob kshglob
setopt nullglob
setopt interactivecomments

# Why in the world is this enabled by default on Linux? zgen?
zmodload -i zsh/stat
disable stat

# enable all of the highlighters
ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor root line)

# secrets
source-with-force ~/.zsh/secrets

# zplug
: ${ZPLUG_HOME:=$XDG_CACHE_HOME/zplug}
: ${ZPLUG_DIR:=$XDG_CACHE_HOME/zplug/repos/zplug/zplug}
: ${ZPLUG_REPOS:=$XDG_CACHE_HOME/zplug/repos}
: ${ZPLUG_CLONE_DEPTH:=1}

if [[ ! -d $ZPLUG_DIR ]]; then
    echo "- No zplug found in \$path or ZPLUG_HOME=$ZPLUG_HOME" >&2

    echo "- Cloning zplug into ZPLUG_HOME=$ZPLUG_HOME" >&2
    if ! git clone ${ZPLUG_REPO:-https://github.com/akatrevorjay/zplug.git} $ZPLUG_HOME/repos/zplug/zplug; then
        echo "- Failed." >&2
        return 1
    fi
fi
source $ZPLUG_DIR/init.zsh || return 1

# TODO check plugins difference on hook and auto-load
source-with-force ${^ZDOTDIRS}/zshplugins(-.)

if ! zplug check --verbose; then
	printf "-- Found changes in zshplugins. Install them? [y/N]: "
    if read -q; then
        echo; zplug install
    fi
fi

zplug load --verbose

# Set up the prompt
case "$USERNAME" in
	root)
		autoload -Uz promptinit
		promptinit
		prompt walters
		;;
	*) source "$ZDOTDIR/themes/airline2.zsh" ;;
esac

# local
# TODO Move to $XDG local path
source-with-force $LOCAL_ZDOTDIR/zshrc

# This is annoying to have earlier.
#setopt warncreateglobal
